# Development + pre-requisite resources

# MCP Server RBAC resources for local development
# Render from Helm chart and filter to only RBAC resources (avoid duplication)
k8s_yaml(local(
  'helm template archestra ../helm/archestra --namespace default --set archestra.orchestrator.kubernetes.mcpServerRbac.create=true --show-only templates/mcp-serviceaccount.yaml',
  quiet=True
))

k8s_resource(
  new_name='mcp-rbac',
  labels=['dev'],
  objects=[
    'archestra-archestra-platform-mcp-k8s-operator:serviceaccount:default',
    'archestra-archestra-platform-mcp-server-operator:role:default',
    'archestra-archestra-platform-mcp-server-operator:rolebinding:default'
  ]
)

# Define is_prod locally (same logic as main Tiltfile)
is_prod = os.getenv('PROD') == 'true'
dev_resource_name = 'pnpm-prod' if is_prod else 'pnpm-dev'

# Build env dict to sync ARCHESTRA_* to NEXT_PUBLIC_ARCHESTRA_* for frontend
# This is rebuilt on each Tiltfile evaluation (triggered by .env changes)
def get_frontend_env():
  env = {
    # Always disable analytics in local development
    'NEXT_PUBLIC_ARCHESTRA_ANALYTICS': 'disabled',
  }

  # Sync ARCHESTRA_* to NEXT_PUBLIC_ARCHESTRA_* if not already set
  sync_vars = [
    'ARCHESTRA_API_BASE_URL',
    'ARCHESTRA_SENTRY_ENVIRONMENT',
    'ARCHESTRA_ENTERPRISE_LICENSE_ACTIVATED',
    'ARCHESTRA_AUTH_DISABLE_BASIC_AUTH',
    'ARCHESTRA_AUTH_DISABLE_INVITATIONS',
  ]

  for var in sync_vars:
    value = os.getenv(var)
    next_public_var = 'NEXT_PUBLIC_' + var
    next_public_value = os.getenv(next_public_var)
    # Only sync if ARCHESTRA_* is set and NEXT_PUBLIC_* is not
    if value and value != '' and not next_public_value:
      env[next_public_var] = value

  return env

frontend_env_vars = get_frontend_env()

local_resource(
  'pnpm-install',
  cmd='CI=true pnpm install',
  cmd_bat='set CI=true && pnpm install',
  deps=[
    '../frontend/package.json',
    '../backend/package.json',
  ],
  labels=['dev']
)

# Main dev/prod bundle
if is_prod:
  local_resource(
    dev_resource_name,
    cmd='rm -rf frontend/.next && pnpm build',
    cmd_bat='if exist frontend\\.next rd /s /q frontend\\.next && pnpm build',
    serve_cmd='pnpm start',
    labels=['dev'],
    resource_deps=['db-migrate'],
    deps=['../.env'],
    env=frontend_env_vars,
    serve_env=frontend_env_vars
  )
else:
  local_resource(
    dev_resource_name,
    serve_cmd='pnpm dev',
    labels=['dev'],
    resource_deps=['db-migrate'],
    deps=['../.env'],
    serve_env=frontend_env_vars
  )

local_resource(
  'lint:fix',
  cmd='pnpm type-check && pnpm lint:fix',
  labels=['dev'],
  resource_deps=['pnpm-install'],
  # Disabled auto-watching to prevent spawning processes on every file change
  # Trigger manually with: tilt trigger lint:fix
  trigger_mode=TRIGGER_MODE_MANUAL,
  auto_init=False,
)

local_resource(
  'lint:fix:unsafe',
  cmd='pnpm lint:fix:unsafe && tilt trigger lint:fix',
  labels=['dev'],
  resource_deps=['pnpm-install'],
  trigger_mode=TRIGGER_MODE_MANUAL,
  auto_init=False,
)

local_resource(
  'codegen',
  cmd='pnpm codegen',
  labels=['dev'],
  resource_deps=['pnpm-install', dev_resource_name],
  trigger_mode=TRIGGER_MODE_MANUAL,
  auto_init=False,
)
